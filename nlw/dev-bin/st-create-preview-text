#!/usr/bin/perl
# @COPYRIGHT@
use strict;
use warnings;
use FindBin;
use lib "$FindBin::Bin/../lib";
use Socialtext::Workspace;
use Socialtext::Hub;
use Socialtext::CLI;
use Socialtext::Encode;
use Fatal qw/opendir closedir chdir open/;
use Cwd qw/abs_path/;

$| = 1; # turn on autoflush

die "Must be run as root!\n" if $< and !$ENV{ST_DEV};
Socialtext::CLI->_drop_privs();

my $only_workspace = shift;
if ($only_workspace) {
    my $wksp = Socialtext::Workspace->new(name => $only_workspace);
    process_workspace($wksp);
}
else {
    my $workspaces = Socialtext::Workspace->All;
    while ( my $wksp = $workspaces->next ) {
	process_workspace($wksp);
    }
}
exit;

sub process_workspace {
    my $wksp = shift;
    print "Storing preview text for workspace " . $wksp->name . ": ";
    my $hub = Socialtext::Hub->new(current_workspace => $wksp);
    my $workspace_dir = Socialtext::Paths::page_data_directory($wksp->name);
    chdir $workspace_dir;
    opendir(my $dfh, $workspace_dir);
    while (my $dir = readdir($dfh)) {
        next unless -d $dir;
        next if $dir =~ m/^\./;

        # Ignore really old pages that have invalid page_ids
        next unless Socialtext::Encode::is_valid_utf8($dir);

        # Check the index.txt page link for a relative link.  Rewrite
        # them to be absolute links.
	my $page_link_name = "$dir/index.txt";
	my $page_link = readlink($page_link_name) 
	    or die "Couldn't readlink $page_link_name: $!";
	unless ($page_link =~ m#^/#) {
	    my $tmp_link = "$page_link_name.tmp";
	    my $abs_page = abs_path("$dir/$page_link");
            die "Could not find symlinked page ($abs_page)" unless -f $abs_page;
	    symlink($abs_page, $tmp_link) 
		or die "Could not symlink $abs_page, $tmp_link: $!";
	    rename $tmp_link => $page_link_name 
		or die "Could not rename $tmp_link, $page_link_name: $!";
	    warn "\nFixed relative symlink $page_link_name\n";
	}
	my $char = '?';
        eval {
            # Ignore non-UTF-8 warnings
	    local $SIG{__WARN__} = sub { 
                my $warning = shift;
                if ($warning =~ m/\Qdoesn't seem to be valid utf-8\E/ or
                    $warning =~ m/\QTreating as iso-8859-1\E/) {
                    $char = '!';  
                }
                else {
                    $char = '';
                    warn "\n\n$warning\n";
                }
            };
	    my $page = $hub->pages->new_from_name($dir);
	    if ($page->metadata->{Summary}) {
		$char = '.';
	    }
	    else {
		my $text = $page->preview_text;
		$page->_store_preview_text($text);
		$char = "<$dir>";
	    }
        };
        print $char;
        warn "Error storing preview text: $@" if $@;
    }
    closedir($dfh);
    print "\n";
}



