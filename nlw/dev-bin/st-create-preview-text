#!/usr/bin/perl
# @COPYRIGHT@
use strict;
use warnings;
use FindBin;
use lib "$FindBin::Bin/../lib";
use Socialtext::Workspace;
use Socialtext::Hub;
use Socialtext::CLI;
use Socialtext::Encode;
use Socialtext::Page::TablePopulator;
use Fatal qw/opendir closedir chdir open/;
use Cwd qw/abs_path/;

$| = 1; # turn on autoflush

die "Must be run as root!\n" if $< and !$ENV{ST_DEV};
Socialtext::CLI->_drop_privs();

if ( @ARGV ) {
    for my $ws_name ( @ARGV ) {
        my $dir = Socialtext::Paths::page_data_directory($ws_name);
        next unless ( -d $dir );
        my $wksp = Socialtext::Workspace->new(name => $ws_name);
        process_workspace($wksp);
    }
}
else {
    my $workspaces = Socialtext::Workspace->All;
    while ( my $wksp = $workspaces->next ) {
	process_workspace($wksp);
    }
}
exit;

sub process_workspace {
    my $wksp = shift;
    print "Storing preview text for workspace " . $wksp->name . ": ";
    my $hub = Socialtext::Hub->new(current_workspace => $wksp);
    my $workspace_dir = Socialtext::Paths::page_data_directory($wksp->name);
    chdir $workspace_dir;
    opendir(my $dfh, $workspace_dir);
    while (my $dir = readdir($dfh)) {
        next unless -d $dir;
        next if $dir =~ m/^\./;

        # Ignore really old pages that have invalid page_ids
        next unless Socialtext::Encode::is_valid_utf8($dir);

        Socialtext::Page::TablePopulator::fix_relative_page_link($dir);

	my $char = '?';
        eval {
            # Ignore non-UTF-8 warnings
	    local $SIG{__WARN__} = sub { 
                my $warning = shift;
                if ($warning =~ m/\Qdoesn't seem to be valid utf-8\E/ or
                    $warning =~ m/\QTreating as iso-8859-1\E/) {
                    $char = '!';  
                }
                else {
                    $char = '';
                    warn "\n\n$warning\n";
                }
            };
	    my $page = $hub->pages->new_page($dir);
	    if ($page->metadata->{Summary}) {
		$char = '.';
	    }
	    else {
		my $text = $page->preview_text;
		$page->_store_preview_text($text);
		#$char = "<$dir>";
		$char = "+";
	    }
        };
        print $char;
        warn "Error storing preview text: $@" if $@;
    }
    closedir($dfh);
    print "\n";
}



