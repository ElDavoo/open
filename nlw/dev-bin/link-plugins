#!/usr/bin/perl
# @COPYRIGHT@
use strict;
use warnings;
use FindBin;
use Fatal qw/chdir mkdir symlink/;
use lib "$FindBin::Bin/../lib";
use File::Find 'find';
use File::Basename qw(basename);
use Socialtext::System qw/shell_run/;
use Term::ANSIColor;

sub usage {
    my $msg = shift;
    warn "$msg\n" if $msg;
    die "USAGE: $0 <plugin list>\n\n";
}

sub link_directory {
    my ($fromdir, $todir, $subdir) = @_;
    return unless -d "$fromdir/$subdir";

    if (!-d $todir) {
        warn "Making directory $todir\n";
        mkdir $todir or die "Cannot mkdir $todir: $!";
    }

    find({
        wanted => sub {
            return if m{/\.svn/};
            return if basename($_) =~ m{^\.};
            my $from = $_;
            (my $to = $from) =~ s{^$fromdir}{$todir};

            if (-d $from) {
                if (!-d $to) {
                    warn "Making directory $to\n";
                    unlink $to if -l $to;
                    mkdir $to or die "Cannot mkdir $to: $!";
                }
            }
            else {
                unlink $to if -l $to;
                error("$to already exists and is a file! Remove it first\n")
                    if -e $to;

                symlink $from, $to 
                    or error("Cannot link $from to $to: $!");

                # add an entry to the local .gitignore if it's missing
                $to =~ s{^$todir/}{};
                unless (`grep $to .gitignore`) {
                    system("echo $to >> .gitignore");
                }
            }
        },
        no_chdir => 1,
    }, "$fromdir/$subdir");
}

usage unless @ARGV;
while (my $plugin = shift) {
    warn "Linking the $plugin plugin\n";
    my $plugin_dir = "$FindBin::Bin/../../plugins/$plugin";
    usage("plugin $plugin doesn't exist") unless -d $plugin_dir;
    shell_run("cd $plugin_dir; make");
    link_directory($plugin_dir, "$FindBin::Bin/..", 'lib');
    link_directory($plugin_dir, "$FindBin::Bin/..", 'bin');
    link_directory($plugin_dir, "$FindBin::Bin/..", 't');
    link_directory($plugin_dir, "$ENV{HOME}/.nlw", 'etc/socialtext/uri_map.d');
}

exit;

sub error {
    my $error = shift;

    print color 'yellow';
    print "\n\n$error\n\n";
    print color 'reset';

    exit;
}
