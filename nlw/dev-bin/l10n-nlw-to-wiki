#!/usr/bin/perl
# @COPYRIGHT@
use strict;
use warnings;
use FindBin qw/$Bin/;
use lib "$Bin/../lib";
use Socialtext::Wikil10n qw/load_existing_l10ns make_rester/;
use Socialtext::System qw/shell_run/;
use Socialtext::File qw/set_contents/;
use Locale::PO;
use File::Find::Rule;
use Cwd;

update_enterprise_features();
link_enterprise_features();

link_enterprise_features();
update_enterprise_features();

my @files = get_files( 
    dirs => [
        "lib/", 
        "share/skin/s2/template", 
        "share/skin/s2/javascript", 
        "share/console-template", 
        "build/templates/socialtext",
    ],
    extra_dirs => [ # glob() files in these symlinks
        "share/template/control",
    ],
    includes => [
        qr#\.(?:pm|js|tt2)$#, 
        qr#share/skin/s2/template#,
        qr#share/template/control#,
        qr#share/console-template/template#,
    ],
    excludes => [
        qr/tiddlytext/,
        qr/socialtext\.js/,
        qr#javascript/test#,
        qr/widget_edit\.tt2/,
    ],
);

my $en_file = "nlw-en.po";
shell_run("dev-bin/l10n-widget-to-js");
call_xgettext( $en_file => @files );
po_to_wiki($en_file);
# unlink $en_file;
exit;

sub update_enterprise_features {
    for my $dir (qw/appliance control/) {
        my $fulldir = readlink "$ENV{ST_CURRENT}/$dir";
        shell_run("svk up -s $fulldir");
    }
}

sub link_enterprise_features {
   _call_link_control_panel();
   _call_link_console();
}

sub _call_link_control_panel {
    my $branch_name = _get_branch_name(); 
    shell_run("dev-bin/link-control-panel $branch_name");
}

sub _call_link_console {
    my $branch_name = _get_branch_name(); 
    shell_run("dev-bin/link-console $branch_name");
}

sub _get_branch_name {
    my $branch_name = "";
    
    my $curdir = cwd;
    my $branch_regexp = qr/$ENV{HOME}\/src\/st\/(.*?)\/nlw/;

    if($curdir =~ m/$branch_regexp/) {
       $branch_name = $1;
    }
    if($branch_name eq "") {
        die "branch isn't found. you should run this script after run cdnlw.";
    }
    return $branch_name; 
}

sub call_xgettext {
    my ( $lang, @files ) = @_;

    my $tmpfile = "/tmp/gettext.$$";
    set_contents( $tmpfile, join( "\n", @files ) );
    unlink $lang;
    shell_run("cat $tmpfile | xargs xgettext.pl -o $lang");
    unlink $tmpfile;
    die "Expected xgettext.pl to create $lang\n" unless -e $lang;
}

sub get_files {
    my %opts = @_;
    my $dirs  = $opts{dirs};
    my $extra_dirs = $opts{extra_dirs};
    my $excludes = $opts{excludes};
    my $includes = $opts{includes};

    my @files = File::Find::Rule->file()->in(@$dirs);
    for my $dir (@{ $opts{extra_dirs} }) {
        push @files, glob("$dir/*");
    }
    my @new_files;

    FILE: for my $file (@files) {
        for my $rx (@$excludes) {
           next FILE if $file =~ $rx;
        }
        for my $rx (@$includes) {
            next unless $file =~ $rx;
            push @new_files, $file;
            last;
        }
    }

    return @new_files;
}


sub po_to_wiki {
    my $pos = Locale::PO->load_file_asarray(shift);
    my $r = make_rester();

    print "Fetching pages tagged l10n...\n";
    my @locale_pages = $r->get_taggedpages('l10n');
    if (@locale_pages == 0) {
        my $page = 'Socialtext - english';
        $r->put_page($page, 'no strings yet...');
        $r->put_pagetag($page, 'l10n');
        $r->put_pagetag($page, 'locale=en');
        push @locale_pages, $page;
    }

    for my $title (@locale_pages) {
        print "Updating localizations for - $title\n";
        my $existing_pos = load_existing_l10ns($r, $title);

        my $new_page = <<EOT;
This page is autogenerated, only edit the 'translation' column.

| *Key* | *Translation* | *Reference* | *Other* |
EOT
        for my $po (@$pos) {
            my $msgid = $po->msgid or die "No msgid?";
            $msgid =~ s/^"(.*)"$/$1/;
            next unless $msgid;
            my $msgstr = $existing_pos->{$msgid}{msgstr} || '';

            # Trim off the line number, because it changes frequently
            # and is very noisy
            (my $ref = $po->reference || '') =~ s/:\d+(\s?)/$1/g;

            $new_page .= "| "
                . join( ' | ', 
                    $msgid,
                    $msgstr,
                    $ref,
                    $po->automatic || '' )
                . " |\n";
        }
        $r->put_page($title, $new_page);
    }
}

