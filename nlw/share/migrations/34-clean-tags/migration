#!/usr/bin/perl
# @COPYRIGHT@
use warnings;
use strict;
use Socialtext::SQL qw/sql_execute sql_singlevalue/;

Remove_duplicate_tags: {
    my $sth = sql_execute(<<EOT);
    SELECT workspace_id, page_id, lower(tag) AS tag
        FROM page_tag JOIN (
            SELECT workspace_id, page_id, lowertag FROM (
                SELECT workspace_id, page_id, lower(tag) AS lowertag, 
                       count(lower(tag)) AS count 
                  FROM page_tag 
                  GROUP BY workspace_id, page_id, lower(tag)
            ) AS foo 
            WHERE count > 1 
            ORDER BY count DESC
        ) AS bar USING (workspace_id, page_id) 
        WHERE lower(tag) = lowertag
        GROUP BY workspace_id, page_id, lower(tag)
EOT

    my $dups = $sth->fetchall_arrayref({});
    print "Removing duplicate tags ...\n" if @$dups;
    for my $row (@$dups) {
        my $tag_sth = sql_execute('
            SELECT tag FROM page_tag
                WHERE workspace_id = ? 
                  AND page_id = ?
                  AND lower(tag) = ?
            ',
            $row->{workspace_id}, $row->{page_id},
            $row->{tag},
        );

        my @tags = sort map { $_->[0] } 
                   @{ $tag_sth->fetchall_arrayref };
        my $to_keep = shift @tags;

        print "  Keeping tag '$to_keep' ($row->{workspace_id}, $row->{page_id}),"
              . " dropping: " . join(', ', map { "'$_'" } @tags) . "\n";
        sql_execute('
            DELETE FROM page_tag
             WHERE workspace_id = ?
               AND page_id = ?
               AND lower(tag) = lower(?)',
            $row->{workspace_id}, $row->{page_id},
            $to_keep
       );
       sql_execute('
           INSERT INTO page_tag VALUES (?, ?, ?)',
            $row->{workspace_id}, $row->{page_id},
            $to_keep,
       );
    }
}

Remove_empty_tags: {
    my $count = sql_singlevalue(
        'SELECT count(*) FROM page_tag WHERE length(tag) = 0',
    );
    if ($count) {
        print "Removing $count empty tags.\n";
        sql_execute(
            'DELETE FROM page_tag WHERE length(tag) = 0',
        );
    }
}
