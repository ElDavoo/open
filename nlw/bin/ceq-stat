#!/usr/bin/env perl
# @COPYRIGHT@
use warnings;
use strict;
use Getopt::Long;
use Pod::Usage 1.12;
use FindBin;
use lib "$FindBin::Bin/../lib";
use Socialtext::Jobs;
use Socialtext::l10n qw/loc/;

GetOptions(
    'h|help|?'      => sub {pod2usage({-verbose => 1, -input => $0}); exit},
    'H|man'         => sub {pod2usage({-verbose => 2, -input => $0}); exit},
) or die;

my %count;
my $jobs = Socialtext::Jobs->new;
for my $type ($jobs->job_types) {
    my @jobs = $jobs->list_jobs(funcname => $type);
    (my $name = $type) =~ s/.+:://;
    $count{$name} = @jobs;
}

my $sum = 0;
print "Jobs in the queue:\n";
for my $name (sort keys %count) {
    next if $name eq 'Test';
    printf "  %15s %5d\n", $name, $count{$name};
    $sum += $count{$name};
}
printf "  %15s %5d\n", loc("Total"), $sum;

=head1 NAME

ceq-stat - Provide a human-readable summary of the jobs in
ceqlotron's queue.

=head1 SYNOPSIS

B<ceq-stat>

=head1 OPTIONS

    --help      This help page
    --man       Manual page

=head1 SAMPLE OUTPUT

 Changes in queue:
   Attachment  1
   Page        5
   Workspace   0

   TOTAL       6

=head1 SEE ALSO

L<celqlotron(1)>, L<ceq-read(1)>

=head1 BUGS

ceq-stat does not lock the job queue directory when scanning for jobs, so occasionally, it
will try and report on a job that disappears out from underneath as it completes processing.
This is known and is done to keep looking at the queue from impacting performance of 
processing the queue.

=cut
