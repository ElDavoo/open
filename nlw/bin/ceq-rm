#!/usr/bin/env perl
# @COPYRIGHT@
use warnings;
use strict;

use Getopt::Long;
use Pod::Usage 1.12;
use FindBin;
use lib "$FindBin::Bin/../lib";
use Socialtext::Jobs;

my %opts;
GetOptions(
    'h|help|?' => sub { pod2usage( { -verbose => 1, -input => $0 } ); exit },
    'H|man'    => sub { pod2usage( { -verbose => 2, -input => $0 } ); exit },
    'class'    => \$opts{class},
    'verbose' => \$opts{verbose},
    'dryrun'  => \$opts{dryrun},
) or die;

{
    no warnings 'once';
    $TheSchwartz::Moosified::FIND_JOB_BATCH_SIZE = 50000;
}


my $Pattern = $ARGV[0]
    || sub { pod2usage( { -verbose => 1, -input => $0 } ); exit };

my $jobs = Socialtext::Jobs->new;
my @job_types = $jobs->job_types;
my @jobs_to_check;
if ($opts{class}) {
    push @jobs_to_check, $jobs->list_jobs(
        funcname => $opts{class},
        want_handle => 1,
    );
}
else {
    for my $type (@job_types) {
        push @jobs_to_check, $jobs->list_jobs(
            funcname => $type,
            want_handle => 1,
        );
    }
}

for my $j (@jobs_to_check) {
    remove_if_match($j);
}
exit;

sub remove_if_match {
    my $job = shift;
    my $args = $job->arg;
    return unless ref($args) eq 'HASH';
    my $target = $job->jobid;
    $target .= ";ws=$args->{workspace_id}" if $args->{workspace_id};
    $target .= ";page=$args->{page_id}" if $args->{page_id};

    if ($target =~ /$Pattern/) {
        if ($opts{dryrun}) {
            warn "would unlink event for $target\n";
        }
        else {
            warn "unlinking event for $target\n" if $opts{verbose};
            $job->completed;
        }
    }
}

=head1 NAME

ceq-rm - Remove change events in the ceqlotron queue according to perl regexp

=head1 SYNOPSIS

B<ceq-rm 'perl regexp'>

=head1 OPTIONS

    --dryrun    Report on what would be removed, but don't actually remove
    --verbose   Report on each file unlinked
    --class     Only look at events of a certain class.
    --help      This help page
    --man       Manual page


=head1 SEE ALSO

L<ceq-stat(1)>, L<ceq-read(1)>, L<ceqlotron(1)>

=cut
